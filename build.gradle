import com.bmuschko.gradle.docker.tasks.image.*

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        classpath 'com.bmuschko:gradle-docker-plugin:3.6.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'com.bmuschko.docker-remote-api'

mainClassName = 'com.agh.iet.komplastech.solver.Main'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://maven.jzy3d.org/releases'
    }
}

shadowJar {
   baseName = 'lib'
}

docker {
    registryCredentials {
        url = 'https://index.docker.io/v1/'
        username = 'kbhit'
        password = file('docker.creds').text
        email = 'gurgul.grzegorz@gmail.com'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task packageToDocker << {
  ['docker/solver/', 'docker/worker/', 'docker/solver-performance/'].each { dest ->
    copy {
      with copySpec {
        from 'build/libs/lib-all.jar'
        rename { 'lib.jar' }
      }
      into dest
    }
  }
}

packageToDocker.dependsOn shadowJar

task buildWorkerImage(type: DockerBuildImage, dependsOn: ["packageToDocker"]) {
    inputDir = file('docker/worker')
    tag = 'kbhit/iga-adi-cl-worker'
}

task buildSolverImage(type: DockerBuildImage, dependsOn: ["packageToDocker"]) {
    inputDir = file('docker/solver')
    tag = 'kbhit/iga-adi-cl-solver'
}

task pushSolverImage(type: DockerPushImage, dependsOn: ["buildSolverImage"]) {
    imageName = "kbhit/iga-adi-cl-solver"
    tag = "latest"
}

task pushWorkerImage(type: DockerPushImage, dependsOn: ["buildWorkerImage"]) {
    imageName = "kbhit/iga-adi-cl-worker"
    tag = "latest"
}

dependencies {
    compile 'org.jzy3d:jzy3d-api:0.9.1'
    compile group: 'com.google.guava', name: 'guava', version: '22.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile "org.jogamp.gluegen:gluegen-rt:2.2.4"
    compile "org.jogamp.jogl:jogl-all:2.2.4"
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'com.beust', name: 'jcommander', version: '1.7'

    compile 'com.hazelcast:hazelcast-kubernetes:1.1.0'
    compile group: 'com.hazelcast', name: 'hazelcast-client', version: '3.10.4'

    testCompile "junit:junit:4.12"
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.5.2'
}